include "taxonomy.diaspec";
//------------------------------------------------------
//                   CONTROLLER
//------------------------------------------------------
controller HandleTimer {
  when provided IsInside
    do ScheduleTimer on Timer;
  when provided PasswordListener
    do ScheduleTimer on Timer; // Password good: stop alarm-timer (1) or ringing-timer (2)
  when provided UpdatePassword
    do ScheduleTimer on Timer;
}

controller HandleNotifier{
  when provided UpdatePassword
    do ScheduleTimer on Timer,
	   SendNonCriticalNotification on Notifier;
  when provided ElapsedTime
    do ScheduleTimer on Timer,
	   SendNonCriticalNotification on Notifier;
}

controller HandlePassword{
  when provided PasswordListener
    do On on Light, 
       Off on Light;
  when provided UpdatePassword
    do On on Light, 
       Off on Light;
}


//------------------------------------------------------
//                    CONTEXT
//------------------------------------------------------
//Permet de savoir si on est INSIDE ou OUTSIDE
context IsInside as Boolean {
  when provided inactivityLevel from InactivitySensor
  get lastInteraction from InactivitySensor
  maybe publish;
}

context ElapsedTime as AlEirbTimer {
	when provided timerTriggered from Timer maybe publish; // listen for all timers

}

context UpdatePassword as PasswordUpdating {
	when provided contact from ContactSensor maybe publish; // publish if setting sensor, else store id
	when provided on from Light maybe publish;
	when provided ElapsedTime maybe publish; // publish update canceling
}

context PasswordListener as Boolean {
	when provided on from Light maybe publish; // store and compare
	when provided contact from ContactSensor maybe publish; // store and compare 
	when provided ElapsedTime no publish; // no more listen after ringing stops
}


structure PasswordUpdating {
	step as UpdatingStep;
	password as String [];
}

enumeration AlEirbTimer {
	DELAY_BEFORE_ALERT, TIMER_UPDATING_PASSWORD
}

enumeration UpdatingStep {
	INIT_UPDATING, VALID_PASS_1, WRONG_PASSWORD, VALID_PASS_2, END_UPDATING, CANCEL_UPDATING
}

